plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation group: "org.apache.commons", name: "commons-compress", version: "1.20"

    testImplementation group: "org.jetbrains.kotlin", name: "kotlin-test"
    testImplementation group: "org.jetbrains.kotlin", name: "kotlin-test-junit"
    testImplementation group: "org.hamcrest", name: "hamcrest", version: "2.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

group "me.honnold"
version "1.0.0"

publishing {
    publications {
        MPQ(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/jhonnold/mpq"

            credentials {
                username = project.hasProperty("github.user") ? project.property("github.user") : System.getenv("GITHUB_USER")
                password = project.hasProperty("github.key") ? project.property("github.key") : System.getenv("GITHUB_KEY")
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintray.user") ? project.property("bintray.user") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintray.key") ? project.property("bintray.key") : System.getenv("BINTRAY_KEY")
    publications = ['MPQ']

    pkg {
        repo = 'mpq'
        name = 'MPQ'
    }
}